# =====================================
# PlayerVaults Created by hrmfulladvocado
# =====================================

options:
    MaxVaults: 1000
    NoPermMsg: &cYou don't have permission for this vault.
    HelpMsg: &cUse /pv <number> to open a vault.
    AdminHelpMsg: &cUse /pvadmin <player> <number> to open another player's vault.
    BlockedError: &cYou cannot store this item in a vault.

# ---------- MAX VAULT CHECK ----------
function getMax(p:player) :: number:
    set {_max} to 0
    loop {@MaxVaults} times:
        set {_i} to loop-number
        if {_p} has permission "playervaults.amount.%{_i}%":
            set {_max} to {_i}
    if {_p} has permission "playervaults.admin":
        return {@MaxVaults}
    return {_max}


function openVault(p:player, target:player, n:number):
    set {_tu} to uuid of {_target}
    set {_max} to getMax({_target})

    if {_n} > {_max}:
        send "{@NoPermMsg}" to {_p}
        stop

    set {_title} to "&7Vault %{_n}%" 

    set {openvault::%uuid of {_p}%::number} to {_n}
    set {openvault::%uuid of {_p}%::target} to {_tu}

    open chest inventory with 6 rows named {_title} to {_p}
    set {_inv} to {_p}'s current inventory

    loop 54 times:
        set {_slot} to loop-number - 1
        if {vaultdata::%{_tu}%::%{_n}%::%loop-number%} is set:
            set slot {_slot} of {_inv} to {vaultdata::%{_tu}%::%{_n}%::%loop-number%}


function saveVault(p:player, inv:inventory):
    set {_pu} to uuid of {_p}
    set {_n} to {openvault::%{_pu}%::number}
    set {_tu} to {openvault::%{_pu}%::target}

    delete {vaultdata::%{_tu}%::%{_n}%::*}

    loop 54 times:
        set {_slot} to loop-number - 1
        set {_item} to slot {_slot} of {_inv}

        if {_item} is not air:
            set {vaultdata::%{_tu}%::%{_n}%::%loop-number%} to {_item}
